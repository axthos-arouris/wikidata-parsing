buildscript {
    repositories {
        jcenter() // only work after gradle 1.7
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://dl.bintray.com/ngbinh/maven/"
        }
    }

    dependencies {
        // scalastyle-plugin for version 2.12 is the only one working 
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.12:1.0.0"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:scalariform-gradle-plugin:0.2.0"
    }
}


plugins {
    id 'java'
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id 'maven'
}


apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'
apply plugin: 'maven-publish'

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.hierynomus.scalariform"
apply plugin: 'scalaStyle'

def date = new Date()
def scalaVersion = "2.11"
def scalaLibraryVersion = "2.11.12"
def dateString = date.format("yyyy.MM.dd.HH.mm")



group = "com.github.axthosarouris"
archivesBaseName = "com.github.axthosarouris.wikidata-parser"
version = "$dateString"

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "4.10.2"
}



repositories {
    jcenter()
    maven { url "https://dl.bintray.com/axthosarouris/scala-libraries" }
}



dependencies {
    implementation group: 'org.scala-lang', name: 'scala-library', version: "$scalaLibraryVersion"
    testImplementation group: 'org.scalatest', name: "scalatest_$scalaVersion", version: '3.0.5'

    implementation "com.github.axthosarouris:scala-commons_${scalaVersion}:2018.12.23"

    implementation ("org.json4s:json4s-jackson_${scalaVersion}:3.5.3") 

    implementation group: 'com.typesafe', name: 'config', version: '1.3.1'
    implementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.pegdown', name: 'pegdown', version: '1.6.0'
}


task spec(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', "build/classes/scala/test", '-o', '-h', 'build/tests/reports']
    classpath = sourceSets.test.runtimeClasspath

}

test.dependsOn(spec)


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


scalariform {
    alignParameters = true
    alignSingleLineCaseStatements = true
    compactStringConcatenation = false
    doubleIndentClassDeclaration = true
    indentLocalDefs = true
    indentSpaces = 2
    preserveDanglingCloseParenthesis = true
    preserveSpaceBeforeArguments =  true
    spaceInsideBrackets = false
}


scalaStyle {
    configLocation = "codestyle/scalastyle_config.xml"
    includeTestSourceDirectory = true
    source = "src/main/scala"
    testSource = "src/test/scala"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']

    pkg {
        repo = 'scala-libraries'
        name = "scala-wikidata-parser_$scalaVersion"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/axthosarouris/wikidata-libraries'

        version {
            name = "$dateString"
            desc = 'Alpha testing'
            released = new Date()
            vcsTag = "$dateString"
//            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }


}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.axthosarouris'
            artifactId "wikidata-parsing_$scalaVersion"
            version "${dateString}"
        }
    }
}

